plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
    id 'com.google.devtools.ksp'
}

android {
    compileSdk 34

    def file = file("version.properties")
    Properties properties = new Properties()
    if (!file.exists()) {
        properties["CODE"] = "1"
        properties["BUILD"] = "0"
        properties.store(file.newWriter(), null)
    }

    def value = 0

    gradle.startParameter.taskNames.each {
        if (it.contains("Release")) value = 1
    }

    def mVersionName = ""
    def mFileName = ""

    if (file.canRead()) {
        properties.load(new FileInputStream(file))

        def mUseIncrement = true
        if (mUseIncrement) {
            properties["CODE"] = (properties["CODE"].toInteger() + value).toString()
            properties["BUILD"] = (properties["BUILD"].toInteger() + 1).toString()
            properties.store(file.newWriter(), null)
        }

        def major = 1
        def minor = 0
        mVersionName = "${major}.${minor}.${properties["CODE"]}.${properties["BUILD"]}"
        mFileName = "${rootProject.name}_${mVersionName}"

        defaultConfig {
            applicationId "com.quixicon"
            minSdkVersion 21
            targetSdkVersion 34
            versionCode properties["CODE"].toInteger()
            versionName "${mVersionName}"
            archivesBaseName = mFileName
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

    } else {
        throw new GradleException("Could not read version.properties")
    }

    buildTypes {

        def SERVER_BASE_URL = "SERVER_BASE_URL"

        configureEach {
            buildConfigField "String", SERVER_BASE_URL, "\"http://quixicon.com\""
            buildConfigField "String", "AMPLITUDE_KEY", "\"\""
            buildConfigField "String", "DRAW_FILE_NAME", "\"tmp_draw.jpg\""
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    flavorDimensions = ["subject"]
    productFlavors {
        en {
            dimension 'subject'
            applicationIdSuffix '.en'
            versionNameSuffix '-en'
        }

        memorycards {
            dimension 'subject'
            applicationId "ru.victorblog.memorycards"
            versionNameSuffix '-q2'
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }

    buildFeatures {
        dataBinding = true
        buildConfig = true
    }

    namespace 'com.quixicon'
}

dependencies {

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "io.reactivex.rxjava2:rxjava:2.2.21"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"

    implementation 'androidx.lifecycle:lifecycle-common-java8:2.6.2'

    implementation 'com.github.bumptech.glide:glide:4.12.0'

    //In-App Reviews
    implementation 'com.google.android.play:core:1.10.3'

    //Dagger
    implementation 'com.google.dagger:dagger:2.48.1'
    implementation 'com.google.dagger:dagger-android:2.48.1'
    implementation 'com.google.dagger:dagger-android-support:2.48.1'
    kapt 'com.google.dagger:dagger-compiler:2.48.1'
    kapt 'com.google.dagger:dagger-android-processor:2.48.1'

    implementation "com.jakewharton.timber:timber:5.0.1"

    implementation 'org.threeten:threetenbp:1.6.8'

    implementation 'androidx.preference:preference-ktx:1.2.1'

    implementation "androidx.room:room-runtime:2.5.2"
    implementation "androidx.room:room-rxjava2:2.5.2"
    implementation "androidx.room:room-ktx:2.5.2"
    ksp "androidx.room:room-compiler:2.5.2"

    //Network
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'

    implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"

    //metrics (firebase, amplitude, branch.io)
    implementation platform('com.google.firebase:firebase-bom:26.6.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-dynamic-links'

    implementation 'com.github.skydoves:balloon:1.6.0'

    //workers
    implementation "androidx.work:work-runtime:2.8.1"
    implementation "androidx.work:work-runtime-ktx:2.8.1"

    //tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation project(':domain')
    implementation project(':data')
    //
    implementation project(':corestub')
}